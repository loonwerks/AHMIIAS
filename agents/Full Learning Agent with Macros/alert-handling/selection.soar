##!
# @file
#
# @created rjones 20210122

echo "\nLoading selection.soar"

##############################
### Selection logic for the warn and do-not-warn operators

### Always choose to warn in the safety condition

proc generateWarnBestSafetyCondition { types } {
    foreach type $types {
        sp "warn*best*safety-condition*${type}
            (state <s> ^operator <o> +)
            (<o> ^name warn)
            (<s> ^${type}-error-info.warn-condition safety)
        -->
            (<s> ^operator <o> >,=)
        "
    }
}
generateWarnBestSafetyCondition { gps lidar imu }

### Always choose NOT to warn in the normal condition

proc generateDoNotWarnBestNormalCondition { types } {
    foreach type $types {
        sp "do-not-warn*best*normal-condition*${type}
            (state <s> ^operator <o> +)
            (<o> ^name do-not-warn)
	        (<s> ^${type}-error-info.value-changed true)
            (<s> ^${type}-error-info <ei>)
            (<ei> ^warn-condition normal)
            (<ei> ^warn-condition <wc>)
        -->
            (<s> ^operator <o> >,=)
            (write (crlf) |proposed do-not-warn in selection for ${type} | <wc> | condition |)
        "
    }
}
generateDoNotWarnBestNormalCondition { gps lidar imu }

################################
### Learning conditions

### if the warn-condition is low or high, then we want to
### learn the user's preferred thresholds for when to warn.
### For this simple case, we will have one rule for each warn condition times one
### rule for each error type, which allows us to learn different thresholds for each
### error type, if we want.
### If we wanted to just learn one threshold across all three error types, we would just
### use two rules.
### If we wanted to distinguish between even more cases (such as different thresholds during
### different flight modes), we would add even more rules.

sp {warn*select*low*gps
    (state <s> ^operator <o> +)
    (<o> ^name warn)
    (<s> ^gps-error-info <ei>)
    (<ei> ^warn-condition low)
-->
    (<s> ^operator <o> = 0.0)
    }

sp {warn*select*low*lidar
    (state <s> ^operator <o> +)
    (<o> ^name warn)
    (<s> ^lidar-error-info <ei>)
    (<ei> ^warn-condition low)
-->
    (<s> ^operator <o> = 0.0)
    }

sp {warn*select*low*imu
    (state <s> ^operator <o> +)
    (<o> ^name warn)
    (<s> ^imu-error-info <ei>)
    (<ei> ^warn-condition low)
-->
    (<s> ^operator <o> = 0.0)
    }

sp {warn*select*high*gps
    (state <s> ^operator <o> +)
    (<o> ^name warn)
    (<s> ^gps-error-info <ei>)
    (<ei> ^warn-condition high)
-->
    (<s> ^operator <o> = 0.0)
    }

sp {warn*select*high*lidar
    (state <s> ^operator <o> +)
    (<o> ^name warn)
    (<s> ^lidar-error-info <ei>)
    (<ei> ^warn-condition high)
-->
    (<s> ^operator <o> = 0.0)
    }

sp {warn*select*high*imu
    (state <s> ^operator <o> +)
    (<o> ^name warn)
    (<s> ^imu-error-info <ei>)
    (<ei> ^warn-condition high)
-->
    (<s> ^operator <o> = 0.0)
    }

### Then we have 6 similar rules for the do-not-warn operator
### Although these are technically not necessary the way we are currently doing things.
### We have no way to get any user feedback when we choose NOT to warn, so these rules
### will never get any reward signal.  However, if we ever get to a point where we could
### give them a reward signal, it would help speed up the learning

sp {do-not-warn*select*low*gps
    (state <s> ^operator <o> +)
    (<o> ^name do-not-warn)
    (<s> ^gps-error-info <ei>)
    (<ei> ^warn-condition low)
-->
    (<s> ^operator <o> = 0.0)
    }

sp {do-not-warn*select*low*lidar
    (state <s> ^operator <o> +)
    (<o> ^name do-not-warn)
    (<s> ^lidar-error-info <ei>)
    (<ei> ^warn-condition low)
-->
    (<s> ^operator <o> = 0.0)
    }

sp {do-not-warn*select*low*imu
    (state <s> ^operator <o> +)
    (<o> ^name do-not-warn)
    (<s> ^imu-error-info <ei>)
    (<ei> ^warn-condition low)
-->
    (<s> ^operator <o> = 0.0)
    }

sp {do-not-warn*select*high*gps
    (state <s> ^operator <o> +)
    (<o> ^name do-not-warn)
    (<s> ^gps-error-info <ei>)
    (<ei> ^warn-condition high)
-->
    (<s> ^operator <o> = 0.0)
    }

sp {do-not-warn*select*high*lidar
    (state <s> ^operator <o> +)
    (<o> ^name do-not-warn)
    (<s> ^lidar-error-info <ei>)
    (<ei> ^warn-condition high)
-->
    (<s> ^operator <o> = 0.0)
    }

sp {do-not-warn*select*high*imu
    (state <s> ^operator <o> +)
    (<o> ^name do-not-warn)
    (<s> ^imu-error-info <ei>)
    (<ei> ^warn-condition high)
-->
    (<s> ^operator <o> = 0.0)
    }
